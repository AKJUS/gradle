// tag::provider-get-task-setup[]
abstract class MyTask extends DefaultTask {
    @Input
    abstract Property<String> getMyInput()

    @OutputFile
    abstract RegularFileProperty getMyOutput()

    @TaskAction
    void doAction() {
        // Use get in the task action
        def outputFile = myOutput.get().asFile
        def outputText = myInput.get()
        println(outputText)
        outputFile.write(outputText)
    }
}
// end::provider-get-task-setup[]

// tag::avoid-this[]
tasks.register("avoidThis", MyTask) {
    myInput = "currentEnvironment=${providers.gradleProperty("currentEnvironment").get()}"  // <1>
    myOutput = new File(layout.buildDirectory.get().asFile, "output-avoid.txt")  // <2>
}
// end::avoid-this[]

// tag::do-this[]
tasks.register("doThis", MyTask) {
    myInput = providers.gradleProperty("currentEnvironment").map { "currentEnvironment=$it" }  // <1>
    myOutput = layout.buildDirectory.file("output-do.txt")  // <2>
}
// end::do-this[]
